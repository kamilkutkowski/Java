/**
 * To clear dependencies use additional parameter --refresh-dependencies
 */

defaultTasks 'clean', 'test', 'aggregate'

repositories {
    mavenLocal()
    jcenter()
}

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.1.1")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'net.serenity-bdd.aggregator'
apply from: "$rootDir/gradle/libraries.gradle"

sourceCompatibility = 1.9
targetCompatibility = 1.9

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

dependencies {
    compile libs.lombok,
            libs.slf4jApi,
            libs.logback,
            libs.serenity.core,
            libs.serenity.junit,
            libs.serenity.cucumber,
            libs.serenity.restAssured,
            libs.junit,
            libs.assertj,
            libs.driverManager
    annotationProcessor libs.lombok
}

task encoding(type: Exec){
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmd', '/c', 'proper-encoding.cmd'
    } else {
        commandLine 'sh', 'proper-encoding.sh'
    }
}

clean.finalizedBy(encoding)

test {
    systemProperty "file.encoding", "utf-8"
    testLogging.showStandardStreams = true
    systemProperties System.getProperties()
}

gradle.startParameter.continueOnFailure = true
test.finalizedBy(aggregate)
